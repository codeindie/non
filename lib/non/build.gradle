configure()

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath 'org.robovm:robovm-gradle-plugin:1.0.0-beta-03'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
        classpath 'org.yaml:snakeyaml:1.14'
    }
}

allprojects {
    version = cfg.version
    
    ext {
        cfg = cfg
        appName = cfg.name
        packageName = cfg.package
        packageDir = cfg.package.replace('.', '/')
        gdxVersion = '1.5.3'
        roboVMVersion = '1.0.0-beta-03'
        box2DLightsVersion = '1.3'
        jrubyVersion = '1.7.19'
        kryonetVersion = '2.22.0-RC1'
        snakeyamlVersion = '1.14'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java"
    
    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"

        try { for(dep in cfg.desktop.libraries) compile dep } catch(e) { }
    }
}

project(":android") {
    apply plugin: 'android-sdk-manager'
    apply plugin: "android"
    
    configurations { natives }

    dependencies {
        compile "com.android.support:multidex:1.0.0"
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"

        try { for(dep in cfg.android.libraries) compile dep } catch(e) { }
    }
}

project(":ios") {
    apply plugin: "java"
    apply plugin: "robovm"
    
    configurations { natives }

    dependencies {
        compile project(":core")
        compile "org.robovm:robovm-rt:${roboVMVersion}"
        compile "org.robovm:robovm-cocoatouch:${roboVMVersion}"
        compile "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-ios"

        try { for(dep in cfg.ios.libraries) compile dep } catch(e) { }
    }
}

project(":core") {
    apply plugin: "java"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        compile "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
        compile "com.esotericsoftware:kryonet:$kryonetVersion"
        compile "org.yaml:snakeyaml:$snakeyamlVersion"
        compile "org.jruby:jruby-core:$jrubyVersion"
        // compile "org.jruby:jruby-stdlib:$jrubyVersion"

        try { for(dep in cfg.libraries) compile dep } catch(e) { }
    }
}

task resolveDependencies << {
    project.rootProject.allprojects.each { subProject ->
        subProject.buildscript.configurations.each { configuration -> configuration.resolve() }
        subProject.configurations.each { configuration -> configuration.resolve() }
    }
}

task update << {
    deldir(file("ios/src"))
    deldir(file("android/src"))
    deldir(file("desktop/src"))
    
    def vals = [
        [ "%APP_NAME%", "%PACKAGE%", "%PACKAGE_DIR%" ],
        [ appName, packageName, packageDir ]
    ]
    
    copyAndReplace("templates/android/strings.xml", "android/res/values/strings.xml", vals)
    copyAndReplace("templates/android/AndroidManifest.xml", "android/AndroidManifest.xml", vals)
    copyAndReplace("templates/android/AndroidLauncher.java", "android/src/$packageDir/android/AndroidLauncher.java", vals)
    copyAndReplace("templates/ios/robovm.properties", "ios/robovm.properties", vals)
    copyAndReplace("templates/ios/IOSLauncher.java", "ios/src/$packageDir/ios/IOSLauncher.java", vals)
    copyAndReplace("templates/desktop/DesktopLauncher.java", "desktop/src/$packageDir/desktop/DesktopLauncher.java", vals)
    
    def src = "../project/non"
    def res = "gen/non"
    def out = "android/assets/non"
    def files = [ "loading.png", "loading_bg.png", "loading_bar.png", "loading_bar_bg.png" ]
    
    for(fl in files) {
        if (file("${src}/${fl}").exists()) {
            copy {
                from src
                into out
                include "${fl}"
            }
        } else {
            copy {
                from res
                into out
                include "${fl}"
            }
        }
    }
    
    copy {
        from "gen/non" 
        into "android/assets/non"
        include "**/*.rb"
    }
    
    copy {
        from "../project"
        into "android/assets"
        exclude "non/"
        exclude "**/*.DS_Store"
        exclude "**/*Thumbs.db"
    }
}

def configure() {
    def f = file("../project/config.yml")

    if (f.exists()) {
        ext.cfg = new org.yaml.snakeyaml.Yaml().load(f.text)
    } else {
        ext.cfg = new org.yaml.snakeyaml.Yaml().load(
            '---\n' +
            '  name: "non"\n' +
            '  package: "non"\n' +
            '  version: "1.0.0"'
        )
    }
}

def copyAndReplace(input, out, values) {
    def total = values[0].size() - 1
    def txt = file(input).text
    
    for(i in 0..total-1) {
        txt = txt.replace(values[0][i], values[1][i])
    }
    
    out = new File(out)
    out.getParentFile().mkdirs()
    out.createNewFile()
    out.write(txt)
}

def deldir(file) {
    if(file.exists()) {
        def files = file.listFiles();
        
        if(files !=null) {
            for(File fl: files) {
                if(fl.isDirectory()) {
                    deldir(fl)
                } else {
                    fl.delete()
                }
            }
        }
    }
        
    file.delete()
}